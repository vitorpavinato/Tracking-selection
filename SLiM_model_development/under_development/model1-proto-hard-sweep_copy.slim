// Model 1 - Hard Sweep: de novo mutation that rapidly reach fixation when it arises;
// N1 - This model explicitly implements 2 temporal samples - works were n < Ne//

// Neutral starts at generation 0 but Beneficial mutation starst at generation 1;
// Neutral mutation has a fixed selection coefficient = 0 and can randomly arise in any chromossome;
// Beneficial mutation, however, happens in a specific point position of the chromossome 2;
// Beneficial mutation also has a fixed selection coefficient;
 
initialize() {
	initializeMutationRate(1e-7); // Florian MS - u=2.5e-8 // Increased to have more mutations
	initializeMutationType("m1", 0.5, "f", 0.0); // neutral //
	initializeMutationType("m2", 0.5, "f", 0.5); // beneficial - same as in Florian MS - unrealistic cap is s=0.5 //
	m1.mutationStackPolicy = "s"; // "s" for default; "f" for the first and "l" for the last //
	m2.mutationStackPolicy = "s"; // "s" for default; "f" for the first and "l" for the last //
	m1.convertToSubstitution = T; // add this to the main model1 code, but not before the 10*Ne equilibrium //
	m2.convertToSubstitution = T; // add this to the main model1 code, but not before the 10*Ne equilibrium //
	m1.color = "gray40";
	m2.color = "green";
	m1.colorSubstitution = "gray20";
	m2.colorSubstitution = "darkgreen";
	
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElementType("g2", m1, 1.0);
	g1.color = "black";
	g2.color = "black";	
	initializeGenomicElement(g1, 0, 49999);
	initializeGenomicElement(g2, 50001, 99999);
	
	initializeRecombinationRate(c(1e-8, 0.5, 1e-8), c(49999, 50000, 99999));	
}

1 {
	sim.chromosome.colorSubstitution = "";
	sim.addSubpop("p0", 1500); // Effective population size - Same as in Florian MS - Ne=1000 - but this is a parameter //
}

15000 late() {
	// Wait until MD-Equilibrium to add the beneficial mutation;
	// It explicitly sample one genome from the entire simulation and add a new/denovo benefitial mutation //
	// It is similar to the "addtion" of a new mutations during gametogenesisthis //
	// This mutation will have higher fitness //
	dnovo = sample(sim.subpopulations.genomes, 1);
	if (dnovo.countOfMutationsOfType(m2) == 0)
		 dnovo.addNewDrawnMutation(m2, 1);
	
	m2.convertToSubstitution = F; // add this to the main model1 code, but not before the 10*Ne equilibrium //
}

15024 late(){
	m1.convertToSubstitution = F; // add this to the main model1 code, but not before the first sampling //
}

15025 late(){ // Here only what is going to be implemented in R //
	
	// BEGIN::Print mutation information //
	u = sim.subpopulations.individuals;
	g = u.genomes;
	m = sortBy(unique(g.mutations), "position"); 
	mp = m.position;
	mi = m.id;
	mf = sim.mutationFrequencies(subpops=NULL,mutations=m);
	mc = m.selectionCoeff;

	chr = ifelse(mp > 50000, paste("chr2"), paste("chr1"));
	mps = sapply(mp, "if (applyValue > 50000) paste(applyValue - 50000); else paste(applyValue);");
	status = repEach(paste("NS"), size(m));
	alleles = repEach(paste("A,T"), size(m));
	
	header = paste("chrom"+"\t"+"position"+"\t"+"muID"+"\t"+"mufreq"+"\t"+"status"+"\t"+"selCoeff"+"\t"+"alleles"+"\n");
	mutInf = paste(chr+"\t"+mps+"\t"+format("%d",mi)+"\t"+format("%.3f",mf)+"\t"+status+"\t"+format("%.3f",mc)+"\t"+alleles+"\n");
	mutInf = header + mutInf;
	writeFile("~/mutInfo_t1.txt", mutInf);
	// END::Print mutation information //
	
	// BEGIN::Print genotypes //
	su = sample(u, 100, F);
	
	genos = NULL; 
	for (si in su)
	{
		hp1 = (asInteger(match(m,sortBy(unique(si.genomes[0].mutations), "position")) >=0)+1);
		hp2 = (asInteger(match(m,sortBy(unique(si.genomes[1].mutations), "position")) >=0)+1);
		
		genoLine = paste(hp1 + '' + hp2, "\t");
		genos = c(genos, genoLine);
	}
	
	genomatrix = paste(genos, "\n");
	writeFile("~/genotypes_t1.txt", genomatrix);
	// END::Print genotypes //
}	

15026 late(){
	m1.convertToSubstitution = T; // add this to the main model1 code, but not before the first sampling //
}

15049 late(){
	m1.convertToSubstitution = F; // add this to the main model1 code, but not before the first sampling //
}

15050 late(){ // Here only what is going to be implemented in R //
	
	// BEGIN::Print mutation information //
	u = sim.subpopulations.individuals;
	g = u.genomes;
	m = sortBy(unique(g.mutations), "position"); 
	mp = m.position;
	mi = m.id;
	mf = sim.mutationFrequencies(subpops=NULL,mutations=m);
	mc = m.selectionCoeff;
	
	chr = ifelse(mp > 50000, paste("chr2"), paste("chr1"));
	mps = sapply(mp, "if (applyValue > 50000) paste(applyValue - 50000); else paste(applyValue);");
	status = repEach(paste("NS"), size(m));
	alleles = repEach(paste("A,T"), size(m));
	
	header = paste("chrom"+"\t"+"position"+"\t"+"muID"+"\t"+"mufreq"+"\t"+"status"+"\t"+"selCoeff"+"\t"+"alleles"+"\n");
	mutInf = paste(chr+"\t"+mps+"\t"+format("%d",mi)+"\t"+format("%.3f",mf)+"\t"+status+"\t"+format("%.3f",mc)+"\t"+alleles+"\n");
	mutInf = header + mutInf;
	writeFile("~/mutInfo_t2.txt", mutInf);
	// END::Print mutation information //

	// BEGIN::Print genotypes //
	su = sample(u, 100, F);
	
	genos = NULL;
	for (si in su)
	{
		hp1 = (asInteger(match(m,sortBy(unique(si.genomes[0].mutations), "position")) >=0)+1);
		hp2 = (asInteger(match(m,sortBy(unique(si.genomes[1].mutations), "position")) >=0)+1);
	
		genoLine = paste(hp1 + '' + hp2, "\t");
		genos = c(genos, genoLine);	
	
	}	
	
	genomatrix = paste(genos, "\n");
	writeFile("~/genotypes_t2.txt", genomatrix);
	// END::Print genotypes //
	
	sim.simulationFinished();	
} 