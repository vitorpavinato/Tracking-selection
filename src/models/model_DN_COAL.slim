function (io)countGametes(io$ x)
	{
		n = sum(sim.subpopulations.individuals.pedigreeParentIDs == x);
		return n;	
	}

initialize() {
	
	// Genome-wide Neutral mutations fitness
	defineConstant("fm1", 0.0);        									// fm1 => the fitness for all neutral mutations "m1" - it is fixed with value 0
	defineConstant("fm2", 0.0);       									// fm2 => the fitness for all neutral mutations "m2" - it is fixed with value 0
	
	// INITIALIZE SIMULATION PARAMETERS
	
	initializeTreeSeq(checkCoalescence=T);
	initializeMutationRate(mu);
	
	// Define the GenomicElementType in the GENOME
	initializeMutationType("m1", dm1, "f", fm1);
	initializeMutationType("m2", dm2, "f", fm2);
	initializeMutationType("m3", dm3, "g", gammaM, gammak);
	m1.mutationStackPolicy = "s";
	m2.mutationStackPolicy = "s";
	m3.mutationStackPolicy = "s";
	m1.convertToSubstitution = T;
	m2.convertToSubstitution = T;
	m3.convertToSubstitution = T;
	initializeGenomicElementType("g1", m1, 1);
	initializeGenomicElementType("g2", c(m2,m3), c((1 - prbe), prbe));
	
	// GENOME's GenomicElementTypes
	
	// Assign each GenomicElementType:
	// g2
	if(!isNULL(g2_idx))
	for(i in g2_idx){
		initializeGenomicElement(g2, asInteger(e_starts[i]), asInteger(e_ends[i]));
	}
	// g1
	if(!isNULL(g1_idx))
	for(j in g1_idx){
		initializeGenomicElement(g1, asInteger(e_starts[j]), asInteger(e_ends[j]));
	}
	
	defineConstant("RECOMBRATES", "initializeRecombinationRate(rr_rates, asInteger(rr_limits));");
	executeLambda(RECOMBRATES);
}

1 {
	sim.addSubpop("p1", Neq);
	cat("mu = " + mu + "\t" + "Neq = " + Neq + "\n");
	
	p1.setSelfingRate(selfing);
	cat("selfing rate = " + selfing + "\n");
	
	sim.rescheduleScriptBlock(s1, start = (10*Neq + 200) -1, end = (10*Neq + 200) -1);
	cat("SIMULATION IS SCHEDULE TO STOP AT GENERATION: " + paste((10*Neq + 200) -1) + "\n");
			
}

1: late() {
	
	// PEDIGREE EFFECTIVE SIZE
	//...................
	
	uniqParents = unique(sim.subpopulations.individuals.pedigreeParentIDs);
	
	gametes = sapply(uniqParents, "countGametes(applyValue);"); // k => Gamete contribution of each parente (g-1 individuals);

	if (size(gametes) > 1)
	{
		ne = (4*p1.individualCount)/(2+var(gametes));
	} else {
		ne = (4*p1.individualCount)/(2+0);
	}
	
	// Save the number of unique parentes IDs
	timene = paste(sim.generation + "\t" + ne);
	
	// Output as a TXT file
	writeFile(filePath=paste0(outputpath + "slim_output_ne1_" + simID + ".txt"), contents=timene , append=T);
	    
	if (sim.treeSeqCoalesced())
   {
		 catn(sim.generation + ": COALESCED");
		
		 // Output a tree file
		 sim.treeSeqOutput(path=paste0(outputpath + "slim_coalesced_DN_" + simID + ".tree"), simplify=F);
			
		 sim.simulationFinished();
	}		
}

s1 late() {
	
	catn("NO COALESCENCE BY GENERATION: " + sim.generation);
	
	// Output a tree file
	sim.treeSeqOutput(path=paste0(outputpath + "slim_coalesced_DN_" + simID + ".tree"), simplify=F);

	sim.simulationFinished();
}


